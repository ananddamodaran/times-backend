buildscript {
    ext.kotlin_version = '1.2.61'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.0'
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.0.0-rc1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    maven {
        url 'https://maven-central.storage.googleapis.com'
    }
    jcenter()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}

def projectId = 'standardenv-gradle'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'
apply plugin: 'com.google.cloud.tools.appengine'



dependencies {

    compile 'javax.servlet:servlet-api:2.5'
    compile 'com.ganyo:gcm-server:1.0.2'
    compile fileTree('lib')
    compile 'com.googlecode.objectify:objectify:5.0.3'

    compile 'com.google.endpoints:endpoints-framework:2.0.8'
    compile 'com.google.endpoints:endpoints-management-control-appengine:1.0.5'
    compile 'com.google.endpoints:endpoints-framework-auth:1.0.5'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile 'com.google.cloud:google-cloud-translate:1.35.0'
}

endpointsServer {
    // Endpoints Framework Plugin server-side configuration
    hostname = "${projectId}.appspot.com"
}


appengine {  // App Engine tasks configuration
    deploy {   // deploy configuration
        version = findProperty("appengine.deploy.version")

        def promoteProp = findProperty("appengine.deploy.promote")
        if (promoteProp != null) {
            promote = new Boolean(promoteProp)
        }
    }
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

// this replaces the ${endpoints.project.id} in appengine-web.xml and web.xml
task replaceProjectId(type: Copy) {
    from 'src/main/webapp/WEB-INF/'
    include '*.xml'
    into "build/exploded-${archivesBaseName}/WEB-INF"
    expand(endpoints:[project:[id:projectId]])
    filteringCharset = 'UTF-8'
}
